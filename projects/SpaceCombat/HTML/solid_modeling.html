<!DOCTYPE html>
<html>
<head>
    <title>05.10 - Custom fragment shader</title>
    <!--script src="../libs/three.js"></script-->
    <script src="https://cdnjs.cloudflare.com/ajax/libs/three.js/r83/three.js"></script>
    <!--script src="../libs/dat.gui.min.js"></script-->
    <style>
        body {
            margin: 0;
            overflow: hidden;
        }
    </style>
</head>



<script id="Scene" type="x-shader/x-fragment">
// ===================== 
//   SCENE
// =====================
Hit intersectScene(Ray ray){

    Hit hit           = Hit(1.0e+5, vec3(0.0), Material(vec3(-1.0), -1.0)); // default hit - background
    Material mat      = Material(vec3(0.5), 0.02);
    
    //Hit hit_1       = intersectSphere( Sphere(1.0, vec3(0.0, 0.0, 0.0), mat ), ray );  hit = closer_hit( hit, hit_1 );
    
    //Hit hit_1       = intersectTorus( Torus(2.0,0.5), ray, mat ); hit = closer_hit( hit, hit_1 );
    
    //=== objects in bounding sphere
    Hit hit_1         = intersectSphere( Sphere(1.0, vec3(0.0, 0.0, 0.0), mat ), ray );
    //hit = closer_hit( hit, hit_1 );
    if( hit_1.t < hit.t ){
        Hit hit_2   = intersectCone( Cone(0.95, 2.0, vec3(0.0, 0.0, 0.0), vec3(0.0, 1.0, 0.0), mat ), ray );  
        hit_2 = closer_hit( hit_2,  intersectSphere( Sphere(0.5, vec3(-0.7, -0.7, 0.0), mat ), ray ) );
        hit_2 = closer_hit( hit_2,  intersectSphere( Sphere(0.5, vec3(-0.7, +0.7, 0.0), mat ), ray ) );
        hit_2 = closer_hit( hit_2,  intersectSphere( Sphere(0.5, vec3(+0.7, -0.7, 0.0), mat ), ray ) );
        hit_2 = closer_hit( hit_2,  intersectSphere( Sphere(0.5, vec3(+0.7, +0.7, 0.0), mat ), ray ) );
        hit = farer_hit( hit_1, hit_2 );
    }
    
    /*
    //===  difference :  sphere1 - sphere2
    Sphere s1       = Sphere(1.0, vec3(0.0, 0.0, 0.0), mat );
    Hit hit_1       = intersectSphere( s1, ray );
    if( hit_1.t < hit.t ){
        vec3 pt = ray.o+hit_1.t*ray.d;
        Sphere s2 = Sphere( 0.5, vec3(+0.7, +0.7, 0.0), mat );
        float side = sphereSide( pt, s2 );
        if( side > 0.0 ){
            hit = hit_1;
        }else{
            Ray ray2 = Ray( pt, ray.d);
            Hit hit_2  = intersectSphere( s2, ray2  );
            if( sphereSide( ray2.o+hit_2.t*ray2.d, s1 ) < 0.0 ) hit = hit_2;
        }
    }
    */
    
    /*
    Sphere s          = Sphere(1., vec3(1., 1., 0.), Material(vec3(0.5), 0.02));
    Plane p           = Plane(0., vec3(0., 1., 0.), Material(vec3(0.5, 0.4, 0.3), 0.02));
    Cone c            = Cone(0.95, 2., vec3(translation, 2., 1.), -axis, Material(vec3(1., 0., 0.), 0.02));    
    Hit hitp = intersectPlane(p, r);  if (hitp.m.f0 >= 0. && hitp.t < hit.t) { hit = hitp; }
    Hit hits = intersectSphere(s, r); if (hits.m.f0 >= 0. && hits.t < hit.t) { hit = hits; }
    Hit hitc = intersectCone(c, r);   if (hitc.m.f0 >= 0. && hitc.t < hit.t) { hit = hitc; }
   */
   
    if( dot( hit.n, ray.d ) > 0.0 ) hit.n *=-1.0;   // double sided surface
    return hit;
}
</script>

<script id="Primitives" type="x-shader/x-fragment">
// ===================== 
//   Primitives
// =====================

struct Material{
    vec3 c;		// diffuse color
    float f0;	// specular color (monochrome)
};

#define PI 3.14159275358979
float hash(float x) { return fract(sin(x) * 43758.5453); }
struct Ray{
    vec3 o;		// origin
    vec3 d;		// direction
};

struct Hit{
    float t;	// solution to p=o+t*d
    vec3 n;		// normal
    Material m;	// material
};
const Hit noHit = Hit(1.0e+10, vec3(0.0), Material(vec3(-1.0), -1.0));

struct Plane{
    float d;	// solution to dot(n,p)+d=0
    vec3 n;		// normal
    Material m;	// material
};

struct Sphere{
	float r;	// radius
    vec3 p;		// center position
    Material m;	// material
};

struct Cone{
	float cosa;	// half cone angle
    float h;	// height
    vec3 c;		// tip position
    vec3 v;		// axis
    Material m;	// material
};

struct Torus{
	float R;	
    float r;	
};

Hit intersectPlane(Plane p, Ray r){
    float dotnd = dot(p.n, r.d);
    if (dotnd > 0.) return noHit;
    float t = -(dot(r.o, p.n) + p.d) / dotnd;
    return Hit(t, p.n, p.m);
}

float sphereSide( vec3 p, Sphere s ){
    vec3 dp = p - s.p;
    return dot(dp,dp)-(s.r*s.r);
}

Hit intersectSphere(Sphere s, Ray r){
	vec3 op = s.p - r.o;
    float b = dot(op, r.d);
    float det = b * b - dot(op, op) + s.r * s.r;
    if (det < 0.) return noHit;
    det = sqrt(det);
    float t = b - det;
    if (t < 0.) t = b + det;
    if (t < 0.) return noHit;
    return Hit(t, (r.o + t*r.d - s.p) / s.r, s.m);
}

Hit intersectCone(Cone s, Ray r){
    vec3 co = r.o - s.c;
    float a = dot(r.d,s.v)*dot(r.d,s.v) - s.cosa*s.cosa;
    float b = 2. * (dot(r.d,s.v)*dot(co,s.v) - dot(r.d,co)*s.cosa*s.cosa);
    float c = dot(co,s.v)*dot(co,s.v) - dot(co,co)*s.cosa*s.cosa;
    float det = b*b - 4.*a*c;
    if (det < 0.) return noHit;
    det = sqrt(det);
    float t1 = (-b - det) / (2. * a);
    float t2 = (-b + det) / (2. * a);
    // This is a bit messy; there ought to be a more elegant solution.
    float t = t1;
    if (t < 0. || t2 > 0. && t2 < t) t = t2;
    if (t < 0.) return noHit;
    vec3 cp = r.o + t*r.d - s.c;
    float h = dot(cp, s.v);
    if (h < 0. || h > s.h) return noHit;
    vec3 n = normalize(cp * dot(s.v, cp) / dot(cp, cp) - s.v);
    return Hit(t, n, s.m);
}

float iTorus( in vec3 ro, in vec3 rd, in float Ra, in float ra ){
	//float Ra2 = torus.x*torus.x;
	//float ra2 = torus.y*torus.y;
    float Ra2 = Ra*Ra;
	float ra2 = ra*ra;
	float m = dot(ro,ro);
	float n = dot(ro,rd);
	float k = (m - ra2 - Ra2)/2.0;
	float a = n;
	float b = n*n + Ra2*rd.z*rd.z + k;
	float c = k*n + Ra2*ro.z*rd.z;
	float d = k*k + Ra2*ro.z*ro.z - Ra2*ra2;
    //----------------------------------
	float p = -3.0*a*a     + 2.0*b;
	float q =  2.0*a*a*a   - 2.0*a*b   + 2.0*c;
	float r = -3.0*a*a*a*a + 4.0*a*a*b - 8.0*a*c + 4.0*d;
	p /= 3.0;
	r /= 3.0;
	float Q = p*p + r;
	float R = 3.0*r*p - p*p*p - q*q;
	float h = R*R - Q*Q*Q;
	float z = 0.0;
	if( h < 0.0 ){
		float sQ = sqrt(Q);
		z = 2.0*sQ*cos( acos(R/(sQ*Q)) / 3.0 );
	}	else{
		float sQ = pow( sqrt(h) + abs(R), 1.0/3.0 );
		z = sign(R)*abs( sQ + Q/sQ );
	}
	z = p - z;
    //----------------------------------
	float d1 = z   - 3.0*p;
	float d2 = z*z - 3.0*r;
	if( abs(d1)<1.0e-4 ){
		if( d2<0.0 ) return -1.0;
		d2 = sqrt(d2);
	}
	else{
		if( d1<0.0 ) return -1.0;
		d1 = sqrt( d1/2.0 );
		d2 = q/d1;
	}
    //----------------------------------
	float t = 1e20;
	h = d1*d1 - z + d2;
	if( h>0.0 ){
		h = sqrt(h);
		float t1 = -d1 - h - a;
		float t2 = -d1 + h - a;
		if( t1>0.0 ){ t=t1; }else if( t2>0.0 ) t=t2;
	}
	h = d1*d1 - z - d2;
	if( h>0.0 ){
		h = sqrt(h);
		float t1 = d1 - h - a;
		float t2 = d1 + h - a;
		if( t1>0.0 ){ t=min(t,t1); }else if( t2>0.0 ) t=min(t,t2);
	}
	return t;
}

vec3 nTorus( in vec3 pos, in float Ra, in float ra ){
    float Ra2 = Ra*Ra;
	float ra2 = ra*ra;
	return normalize( pos*(dot(pos,pos)-ra2 - Ra2*vec3(1.0,1.0,-1.0)));
}

Hit intersectTorus(Torus tor, Ray ray, Material mat ){
    float t = iTorus( ray.o, ray.d, tor.R, tor.r );
    //if( t>0.0 && t<100.0 ){ 
    if( t>0.0 ){ 
        vec3  n = nTorus( ray.o + t*ray.d, tor.R, tor.r ); 
        return Hit(t, n, mat);
    } else{
        return noHit;
    }
}


Hit closer_hit( Hit h1, Hit h2 ){
    if(h1.t<h2.t){ return h1; }else{ return h2; }
}
Hit farer_hit( Hit h1, Hit h2 ){
    if(h1.t>h2.t){ return h1; }else{ return h2; }
}
</script>

<script id="RayTracer" type="x-shader/x-fragment">
// ===================== 
//   RayTracer
// =====================
uniform vec2  resolution;
uniform float time;
uniform mat3  camMat;

#define MAX_BOUNCES 2
float gamma = 2.2;

struct DirectionalLight{
    vec3 d;		// Direction
    vec3 c;		// Color
};

DirectionalLight sunLight = DirectionalLight(normalize(vec3(1., .5, .5)), vec3(1e3));
vec3 skyColor(vec3 d){
    float transition = pow(smoothstep(0.02, .5, d.y), 0.4);
    vec3 sky = 2e2*mix(vec3(0.52, 0.77, 1), vec3(0.12, 0.43, 1), transition);
    vec3 sun = sunLight.c * pow(abs(dot(d, sunLight.d)), 5000.);
    return sky + sun;
}

float pow5(float x) { return x * x * x * x * x; }

// Schlick approximation
float fresnel(vec3 h, vec3 v, float f0){
    return pow5(1. - clamp(dot(h, v), 0., 1.0)) * (1. - f0) + f0;
}

float epsilon = 4e-4;

vec3 accountForDirectionalLight(vec3 p, vec3 n, DirectionalLight l){
    if (intersectScene(Ray(p + epsilon * l.d, l.d)).m.f0 < 0.0){
        return clamp(dot(n, l.d), 0.0, 1.0) * l.c;
    }
	return vec3(0.0);
}

vec3 radiance(Ray r){   
    vec3 accum = vec3(0.);
    vec3 filter = vec3(1.);
    for (int i = 0; i <= MAX_BOUNCES; ++i){
        Hit hit = intersectScene(r);
        if (hit.m.f0 >= 0.){
            float f = fresnel(hit.n, -r.d, hit.m.f0);
            vec3 hitPos = r.o + hit.t * r.d;
            // Diffuse
            vec3 incoming = vec3(0.);
            incoming += accountForDirectionalLight(hitPos, hit.n, sunLight);
            accum += (1. - f) * filter * hit.m.c * incoming;
            // Specular: next bounce
            filter *= f;
            vec3 d = reflect(r.d, hit.n);
            r = Ray(r.o + hit.t * r.d + epsilon * d, d);
        }else{
            accum += filter * skyColor(r.d);
            break;
        }
    }
    return accum;
}

// See: http://filmicgames.com/archives/75
vec3 toneFunc(vec3 color){
	float A = 0.15;
	float B = 0.50;
	float C = 0.10;
	float D = 0.20;
	float E = 0.02;
	float F = 0.30;
	float W = 11.2;
	float exposure = 0.012;
	color *= exposure;
	color = ((color * (A * color + C * B) + D * E) / (color * (A * color + B) + D * F)) - E / F;
	float white = ((W * (A * W + C * B) + D * E) / (W * (A * W + B) + D * F)) - E / F;
	color /= white;
	color = pow(color, vec3(1. / gamma));
	return color;
}

vec3 HighQualityRender( vec3 uvz, mat3 camMat_ ){
	// Multi-sample antia-aliasing
    float o1 = 0.25;
    float o2 = 0.75;
    vec2 msaa[4];
    msaa[0] = vec2( o1,  o2);
    msaa[1] = vec2( o2, -o1);
    msaa[2] = vec2(-o1, -o2);
    msaa[3] = vec2(-o2,  o1);
    vec3 color = vec3(0.);
    for (int i = 0; i < 4; ++i){
        vec3 p0 = vec3(0.0, 0.0, 10.0 );
        vec3 p = p0;
        vec3 offset = vec3(msaa[i] / resolution.y, 0.0);
        vec3 d = normalize(vec3(resolution.x/resolution.y * uvz.x, uvz.y, -uvz.z ) + offset);
        Ray r = Ray(camMat_*p, camMat_*d);
        color += radiance(r) / 4.0;
    }
    return toneFunc(color);
}

vec3 LowQualityRender( vec3 uvz, mat3 camMat_  ){
    vec3 p  = vec3(0.0, 0.0, 10.0 );
    vec3 d  = normalize(vec3(resolution.x/resolution.y * uvz.x, uvz.y, -uvz.z ) );
    Ray r   = Ray(camMat_*p, camMat_*d);
	Hit hit = intersectScene(r);
    if( hit.t<1.0e+4 ){
        float c_diffuse = clamp(dot(hit.n, sunLight.d) * 0.001, 0.0, 1.0);
        return c_diffuse * sunLight.c + vec3(0.1,0.1,0.2);    
    }else{
        discard;
    }
}

void main( ){	
	vec3 uvz = vec3( 2.0 * gl_FragCoord.xy / resolution.xy - 1.0, 5.0 );
    
    //gl_FragColor    = vec4( LowQualityRender (uvz,camMat), 1.0 ); 
    gl_FragColor    = vec4( HighQualityRender(uvz,camMat), 1.0 ); 
    
}
</script>

<script>
	// ===================== 
    //   THREE JS MAIN
    // =====================
    // global variables
    var renderer;
    var scene;
    var camera;

    var uniforms = {};
    var control;
    var mousePosOld;
    
    var camQuat = new  THREE.Quaternion();
    //var camMat  = new THREE.Matrix3();
    
    function handleMouseMove(event) {
        var dot, eventDoc, doc, body, pageX, pageY;        
        if (mousePosOld) {
            var dx = (event.clientX-mousePosOld.x)*1.0;
            var dy = (event.clientY-mousePosOld.y)*1.0;
            //console.log( "x: "+ dx +" y: "+ dy );
            //var v = new THREE.Vector3( dx, dy, 0.0 );//.normalize();
            //var q = new THREE.Quaternion().setFromEuler( v );
            //THREE.Quaternion.slerp( camQuat, q, camQuat, 0.07 );
            //var q = new THREE.Quaternion( 0.01, 0.01, 0.01, 1.0 );
            var q = new THREE.Quaternion( -dy*0.002, 0.0, dx*0.002, 1.0 );
            camQuat.multiply(q).normalize();
            //console.log( q.x+" "+ q.y+" "+ q.z+" "+ q.w +" "+ camQuat.x+" "+ camQuat.y+" "+ camQuat.z+" "+ camQuat.w  );
            
        }else{
            mousePosOld = new THREE.Vector3();
        }
        mousePosOld.x = event.clientX;
        mousePosOld.y = event.clientY;
        
    }

    function init() {
        document.onmousemove = handleMouseMove;
    
        scene = new THREE.Scene();
        camera = new THREE.PerspectiveCamera(45, window.innerWidth / window.innerHeight, 0.1, 1000);
        renderer = new THREE.WebGLRenderer();
        renderer.setClearColor(0x000000, 1.0);
        renderer.setSize(window.innerWidth, window.innerHeight);

        // SHADER
        var basicShader = THREE.ShaderLib['normal'];
		uniforms = {
				time      : { value: 1.0 },
				resolution: { value: new THREE.Vector2() },
				camMat    : { value: new THREE.Matrix3() }
		};
		uniforms.resolution.value.x = renderer.domElement.width;
		uniforms.resolution.value.y = renderer.domElement.height;
        var material = new THREE.ShaderMaterial({
            uniforms: uniforms,
            vertexShader: basicShader.vertexShader,
            fragmentShader: document.getElementById('Primitives').text + document.getElementById('Scene').text + document.getElementById('RayTracer').text,
        });
                
        var mesh = new THREE.Mesh( new THREE.PlaneBufferGeometry( 200, 200 ), material ); scene.add( mesh );
				
        camera.position.x = 0.0;
        camera.position.y = 0.0;
        camera.position.z = 100.0;
        camera.lookAt(scene.position);
        
        document.body.appendChild(renderer.domElement);
        control = new function () {
            this.rotationSpeed = 0.005;
            this.scale = 1;
        };
        //addControls(control);
        // call the render function
        render();
    }

/*
    function addControls(controlObject) {
        var gui = new dat.GUI();
        gui.add(controlObject, 'rotationSpeed', -0.1, 0.1);
        gui.add(controlObject, 'scale', 0.01, 2);
    }
*/

    function render() {
        //console.log("x: " + event.clientX + ", y: " + event.clientY );
        
        var camMat4 = new THREE.Matrix4(); camMat4.compose ( new THREE.Vector3(0.0,0.0,0.0), camQuat, new THREE.Vector3(1.0,1.0,1.0) );
        var camMat_ = new THREE.Matrix3(); camMat_.getNormalMatrix ( camMat4 );
        //console.log( camMat_.elements +" "+ camQuat.x+" "+ camQuat.y+" "+ camQuat.z+" "+ camQuat.w  );
        uniforms.camMat.value = camMat_;
        
        renderer.render(scene, camera);
        //scene.getObjectByName('cube').rotation.x += control.rotationSpeed;
        //scene.getObjectByName('cube').scale.set(control.scale, control.scale, control.scale);
        uniforms.time.value += 0.05;
        requestAnimationFrame(render);
    }

    // calls the init function when the window is done loading.
    window.onload = init;

</script>
<body>
See: 
https://www.shadertoy.com/user/Zavie <br>
    Minimal Raytracer : https://www.shadertoy.com/view/XsSSWW <br>
    Cone RayTrace with Radiance : https://www.shadertoy.com/view/MtcXWr <br>
</body>
</html>
